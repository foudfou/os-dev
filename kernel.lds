/**
 * Kernel basic linker script, following the OSDev wiki on
 * https://wiki.osdev.org/Bare_Bones.
 *
 * man in `info ld Scripts`
 */


/** Starts execution at the '_start' symbol as defined in `boot.s`. */
ENTRY(_start)


/** Sections layout. */
SECTIONS
{
    /**
     * Kernel's booting code could be loaded starting at 1KiB address following
     * the bootloader convention.
     */
    . = 0x1000;

    /* Take note of the beginning of the kernel. This only makes sense if
       output supports symbol, like elf, unlike binary! */
    __k_start = .;

    .text ALIGN(4K):    /** Align to 4KiB boundary. */
    {
        KEEP(*(.multiboot))     /** Put multiboot header before code. */
        *(.text)
        *(.comment)
    }

    .rodata ALIGN(4K):
    {
        *(.rodata)
    }

    .data ALIGN(4K):
    {
        *(.data)
    }

    .bss ALIGN(4K):
    {
        *(.bss)     /** Includes our 16KiB temporary stack. */
        *(COMMON)
    }

    /* Take note of the end of the kernel */
    __k_end = .;

    /* We don't care of the note, indent, comment, etc.. sections generated by gcc */
    /DISCARD/ :{
        *(.note*)
        *(.indent)
        *(.comment)
        *(.stab)
        *(.stabstr)
    }
}
